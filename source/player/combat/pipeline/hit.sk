function on_hit(ctx: CombatContext struct):
    if any:
        {_ctx}->attacker = {_ctx}->victim
        {_ctx}->victim is a display, an interaction
        handle_hit_frames({_ctx}) = true
    then:
        stop

    process_stats({_ctx}, "hit_modifiers")

    a_damage_indicator({_ctx})

    if {_ctx}->miss = true:
        fx_miss({_ctx})
        stop

    fx_hit({_ctx})

    on_knockback({_ctx})
    on_damage({_ctx})

    fx_impact_frames({_ctx})

function fx_hit(ctx: CombatContext struct):
    set {_hit} to {_ctx}->hit
    create custom crit:
        count: 5
        velocity: outwards
        extra: 0.2
    a_particle({_hit}, last created particle)
    create custom electric_spark:
        count: 3
        velocity: outwards
        extra: 0.2
    a_particle({_hit}, last created particle)
    a_particle({_hit}, vfx_effect_spell(5, red))
    draw 5 effect using particleSpell(red, 1) at {_hit} with delta vec(1, 0, 1) with extra 1