using for loops
using script reflection

function handle_hotkey_event(player: player, event: string, item: item = {_}):
    # removes the chance for double hotkeys, e.g drop & left click
    stop if is_on_cooldown("last_hotkey", {_player}) = true
    set_cooldown("last_hotkey", 1 tick, {_player})

    # incase the tool is null when called (drop)
    set {_weapon} to ({_item} ? tool of {_player})

    # all SpecialEffect's that have an extension with this hook 
    set {_hotkeys::*} to (item({_weapon})->special_effects) where [input->extension has field {_event}]

    for {_hotkey} in {_hotkeys::*}:
        set {_cooldown_key} to "special_effect:%{_hotkey}->extension%"
        if is_on_cooldown({_cooldown_key}, {_player}) = true:
            continue

        # run hook
        run {_hotkey}->extension's field {_event} with args {_player}, {_hotkey}->extension

        # account for cooldown behind scenes
        if {_hotkey}->extension doesn't have field "cooldown":
            continue
    
        set_cooldown({_cooldown_key}, {_hotkey}->extension->cooldown, {_player})