options:
    c1: (1.701575)
    c2: (1.701575 * 1.525)
    c3: (2.701575)
    c4: ((2 * PI) / 3)
    c5: ((2 * PI) / 4.5)

function ease_none(p: number) returns number:
    return 1

function ease_linear(p: number) returns number:
    return {_p}

function ease_in_quad(p: number) returns number:
    return {_p} ^ 2

function ease_out_quad(p: number) returns number:
    return 1 - ((1 - {_p}) ^ 2)

function ease_in_out_quad(p: number) returns number:
    return (2 * ({_p} ^ 2)) if ({_p} < 0.5), else (1 - ((((-2 * {_p}) + 2) ^ 2) / 2))

function ease_in_cubic(p: number) returns number:
    return {_p} ^ 3

function ease_out_cubic(p: number) returns number:
    return 1 - ((1 - {_p}) ^ 3)

function ease_in_out_cubic(p: number) returns number:
    return (4 * ({_p} ^ 3)) if ({_p} < 0.5), else (1 - ((((-2 * {_p}) + 2) ^ 3) / 2))

function ease_in_quart(p: number) returns number:
    return {_p} ^ 4

function ease_out_quart(p: number) returns number:
    return 1 - ((1 - {_p}) ^ 4)

function ease_in_out_quart(p: number) returns number:
    return (8 * ({_p} ^ 4)) if ({_p} < 0.5), else (1 - ((((-2 * {_p}) + 2) ^ 4) / 2))

function ease_in_quint(p: number) returns number:
    return {_p} ^ 5

function ease_out_quint(p: number) returns number:
    return 1 - ((1 - {_p}) ^ 5)

function ease_in_out_quint(p: number) returns number:
    return (16 * ({_p} ^ 5)) if ({_p} < 0.5), else (1 - ((((-2 * {_p}) + 2) ^ 5) / 2))

function ease_in_sine(p: number) returns number:
    return 1 - cos(({_p} * PI radians) / 2)

function ease_out_sine(p: number) returns number:
    return sin(({_p} * PI radians) / 2)

function ease_in_out_sine(p: number) returns number:
    return (-1 * (cos(PI radians * {_p}) - 1)) / 2

function ease_in_expo(p: number) returns number:
    return 0 if ({_p} = 0), else (2 ^ ((10 * {_p}) - 10))

function ease_out_expo(p: number) returns number:
    return 1 if ({_p} = 1), else (1 - (2 ^ (-10 * {_p})))

function ease_in_out_expo(p: number) returns number:
    if {_p} = 0:
        return 0
    else if {_p} = 1:
        return 1
    else if {_p} < 0.5:
        return (2 ^ (20 * {_p} - 10)) / 2
    return (2 - (2 ^ (-20 * {_p} + 10))) / 2

function ease_in_circle(p: number) returns number:
    return 1 - sqrt(1 - ({_p} ^ 2))

function ease_out_circle(p: number) returns number:
    return sqrt(1 - (({_p} - 1) ^ 2))

function ease_in_out_circle(p: number) returns number:
    if {_p} < 0.5:
        return (1 - sqrt(1 - ((2 * {_p}) ^ 2))) / 2
    return (sqrt(1 - ((-2 * {_p} + 2) ^ 2)) + 1) / 2

function ease_in_back(p: number) returns number:
    return {@c3} * ({_p} ^ 3) - {@c1} * ({_p} ^ 2)

function ease_out_back(p: number) returns number:
    return (1 + ({@c3} * (({_p} - 1) ^ 3)) + {@c1} * (({_p} - 1) ^ 2))

function ease_in_out_back(p: number) returns number:
    if {_p} < 0.5:
        return (((2 * {_p}) ^ 2) * (({@c2} + 1) * 2 * {_p} - {@c2})) / 2
    return (((2 * {_p} - 2) ^ 2) * (({@c2} + 1) * ({_p} * 2 - 2) + {@c2}) + 2) / 2

function ease_in_elastic(p: number) returns number:
    if {_p} = 0:
        return 0
    else if {_p} = 1:
        return 1
    return (-1 * 2 ^ (10 * {_p} - 10)) * sin((({_p} * 10 - 10.75) * {@c4}) radians)

function ease_out_elastic(p: number) returns number:
    if {_p} = 0:
        return 0
    else if {_p} = 1:
        return 1
    return (2 ^ (-10 * {_p})) * sin((({_p} * 10 - 0.75) * {@c4}) radians) + 1

function ease_in_out_elastic(p: number) returns number:
    if {_p} = 0:
        return 0
    else if {_p} = 1:
        return 1
    else if {_p} < 0.5:
        return (-1 * ((2 ^ (20 * {_p} - 10)) * sin(((20 * {_p} - 11.125) * {@c5}) radians))) / 2
    return ((2 ^ (-20 * {_p} + 10)) * sin(((20 * {_p} - 11.125) * {@c5}) radians)) / 2 + 1

function ease_in_bounce(p: number) returns number:
    return 1 - ease_out_bounce(1 - {_p})

function ease_out_bounce(p: number) returns number:
    set {_n1} to 7.5625
    set {_d1} to 2.75

    if ({_p} < (1 / {_d1})):
        return (({_n1} * {_p}) * {_p})
    else if ({_p} < (2 / {_d1})):
        subtract (1.5 / {_d1}) from {_p}
        return ((({_n1} * {_p}) * {_p}) + 0.75)
    else if ({_p} < (2.5 / {_d1})):
        subtract 2.25 / {_d1} from {_p}
        return ((({_n1} * {_p}) * {_p}) + 0.9375)

    subtract (2.625 / {_d1}) from {_p}
    return ((({_n1} * {_p}) * {_p}) + 0.984375)

function ease_in_out_bounce(p: number) returns number:
    if ({_p} < 0.5):
        return ((1 - ease_out_bounce(1 - (2 * {_p}))) / 2)
    return ((1 + ease_out_bounce((2 * {_p}) - 1)) / 2)