# This module provides utilities for defining, sequencing, and playing sounds, patterns, and sound layers
# Supports defining sound configurations, arranging them into patterns, and scheduling their play timings
# Useful for timing-based sound sequences, layered sound patterns, and generative sound effects
#
# This module requires the following other modules: NONE

using script reflection
using for loops

# STRUCTS

#> Represents a sound configuration with properties for how it should be played
struct Sound:
	id: string                        # The Minecraft sound ID
	volume: number                    # Volume to play at
	pitch: number                     # Pitch to play at
	seed: integer = {_}               # Optional seed
	category: sound category = master category # Sound category for context
	offset: timespan = 0 ticks        # Delay from the start of the pattern


#> Represents a sound pattern made up of multiple steps and its total duration
struct SoundPattern:
	id: string                         # Unique pattern identifier
	steps: Sound structs                     # Collection of pattern_step structs
	total_duration: timespan = 0 ticks # Total length of the pattern (computed)

#> Represents a pattern layer, consisting of a pattern and specific beat timings to trigger it at
struct PatternLayer:
	pattern: SoundPattern struct # pattern to trigger
	beats: numbers     # List of beat offsets (relative to base beat duration)

# FUNCTIONS

#> Plays a sound struct to the given players
#> This is mostly internal, you probably want to use the `sound` function instead
#>
#> @param players List of players to hear the sound
#> @param sound A sound struct defining how the sound should play
#> @param after (Optional) Delay before play
function sound(players: players, sound: Sound struct):
	wait {_sound}->offset
	play sound ({_sound}->id) with seed ({_sound}->seed) in ({_sound}->category) at volume ({_sound}->volume) at pitch ({_sound}->pitch) to {_players::*}

#> Defines a new sound struct and registers it under an ID
#>
#> @param sound_id The Minecraft sound ID
#> @param volume (Optional) Volume to play at, default 1
#> @param pitch (Optional) Pitch to play at, default 1
#> @param seed (Optional) Seed for variations of sounds, default random which is <none>
#> @param category (Optional) Sound category, default master
#> @param offset (Optional)
function a_sound(sound_id: string, volume: number = 1, pitch: number = 1, seed: integer = {_}, category: sound category = {_}, offset: timespan = {_}) :: Sound struct:
	return Sound struct instance:
		id: {_sound_id}
		volume: {_volume}
		pitch: {_pitch}
		seed: {_seed}
		category: {_category}
		offset: {_offset}

#> Creates and returns a PatternLayer struct
#>
#> @param pattern SoundPattern struct to use
#> @param beats A list of beat positions (numbers) to schedule pattern play timings
#> @return The created PatternLayer struct
function a_pattern_layer(pattern: SoundPattern struct, beats: numbers) :: PatternLayer struct:
	set {_layer} to PatternLayer struct instance:
		pattern: {_pattern}
		beats: {_beats::*}
	return {_layer}

#> Plays one or more pattern layers for players, using a base beat duration
#>
#> @param p List of players to hear the pattern layers
#> @param layers A collection of PatternLayer structs
#> @param beat_duration Time duration for one beat
function play_pattern_layer(p: players, layers: PatternLayer structs, beat_duration: timespan):
	for {_layer} in {_layers::*}:
		for {_beat} in {_layer}->beats:
			set {_timing} to {_beat_duration} * {_beat}
			play_pattern({_p::*}, {_layer}->pattern, {_timing})

#> Plays a sound pattern after an optional delay
#>
#> @param p List of players to hear the pattern
#> @param pattern The pattern to trigger
#> @param after (Optional) Delay before starting pattern
function play_pattern(p: players, pattern: SoundPattern struct, after: timespan = 0 ticks):
	wait {_after}
	for {_sound} in {_pattern}->steps:
		sound({_p::*}, {_sound})

#> Repeats a sound multiple times at a fixed interval
#> Optionally transforms the sound struct each time before playing
#>
#> @param p List of players
#> @param id Sound ID to play
#> @param repeat Number of repetitions
#> @param interval (Optional) Delay between each repeat
#> @param transformer (Optional) A function to modify the sound before each play
function sound_repeat(p: players, sound: Sound struct, repeat: integer, interval: timespan = {_}, transformer: function = {_}):
	set {_sound} to struct copy of {_sound}
	for {_i} in {_repeat} times:
		wait {_interval}
		if {_transformer} is set:
			set {_sound} to result of {_transformer} with args ({_sound}, {_i})
		sound({_p::*}, {_sound})
